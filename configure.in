AC_INIT(gtkada,16.1.0,report@adacore.com,gtkada)
AC_CANONICAL_SYSTEM

TARGET=""
if test x$host != x$target ; then
   TARGET=${target}
fi
AC_SUBST(TARGET)

AM_ADD_OS_SPECIFIC_FLAGS

# checking for local tools
AC_PROG_CC

case $build_os in
   *darwin*) AC_PROG_OBJC(clang gcc CC) ;;
   *) OBJC=""; AC_SUBST(OBJC) ;;
esac

AM_PATH_GNAT

CHECK_BUILD_TYPE     # are we in devel mode ?

# Gettext stuff
ALL_LINGUAS="en"
AM_GNU_GETTEXT([external])

# checking for gtk
AM_PATH_GTK(3.14.0)

# try to find OpenGL if present
AM_CHECK_OPENGL

# Ignore user's choice of libdir, as well as configure's own default,
# because we want GtkAda to always install in /lib (backward compatibility)

libdir='${prefix}/lib'

AC_OUTPUT(Makefile shared.gpr po/Makefile src/config.h)

AC_MSG_NOTICE([--------- Summary for Gtkada $PACKAGE_VERSION -----------------])
AC_MSG_NOTICE(AC_HELP_STRING(
              [Shared libraries:],[$CAN_BUILD_SHARED (default: $DEFAULT_LIBRARY_TYPE)]))
AC_MSG_NOTICE(AC_HELP_STRING([OpenGL:],[$HAVE_OPENGL]))
AC_MSG_NOTICE([--------------------------------------------])
